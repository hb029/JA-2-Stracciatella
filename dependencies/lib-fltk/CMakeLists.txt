include(ExternalProject)

externalproject_add(fltk-internal
    URL "http://fltk.org/pub/fltk/1.3.4/fltk-1.3.4-2-source.tar.gz"
    INSTALL_DIR "${CMAKE_BINARY_DIR}/lib-fltk"
    CMAKE_ARGS
        ${EXTERNAL_PROJECT_CMAKE_ARGS}
        -DCMAKE_INSTALL_PREFIX=${CMAKE_BINARY_DIR}/lib-fltk
        -DOPTION_USE_GL=OFF -DOPTION_USE_SYSTEM_ZLIB=OFF -DOPTION_USE_SYSTEM_LIBJPEG=OFF
        -DOPTION_USE_SYSTEM_LIBPNG=OFF -DOPTION_USE_XINERAMA=OFF -DOPTION_USE_XFIXES=OFF
        -DOPTION_USE_XCURSOR=OFF -DOPTION_USE_XFT=OFF -DOPTION_USE_XRENDER=OFF
        -DOPTION_USE_XDBE=OFF -DOPTION_BUILD_EXAMPLES=OFF
)

set(FLTK_BASE_DIR "${CMAKE_BINARY_DIR}/lib-fltk")
set(FLTK_LIB_DIR "lib")
set(FLTK_LIB_PREFIX "${CMAKE_STATIC_LIBRARY_PREFIX}")
set(FLTK_LIB_SUFFIX "${CMAKE_STATIC_LIBRARY_SUFFIX}")
set(FLTK_ADDITIONAL_LIBRARIES "")

if (UNIX AND NOT MINGW AND NOT APPLE)
    set(FLTK_LIB_DIR "lib64")
    set(FLTK_ADDITIONAL_LIBRARIES X11)
elseif(MINGW)
    set(FLTK_ADDITIONAL_LIBRARIES comctl32)
elseif(MSVC)
    set(MSVC_LIB_SUFFIX "")
    if (CMAKE_BUILD_TYPE STREQUAL "Debug")
        set(MSVC_LIB_SUFFIX "d")
    endif()
    set(FLTK_LIB_SUFFIX "${MSVC_LIB_SUFFIX}${FLTK_LIB_SUFFIX}")
endif()

set(FLTK_INCLUDE_DIR "${CMAKE_BINARY_DIR}/lib-fltk/include" PARENT_SCOPE)
set(FLTK_LIBRARIES
    "${FLTK_BASE_DIR}/${FLTK_LIB_DIR}/${FLTK_LIB_PREFIX}fltk_images${FLTK_LIB_SUFFIX}"
    "${FLTK_BASE_DIR}/${FLTK_LIB_DIR}/${FLTK_LIB_PREFIX}fltk${FLTK_LIB_SUFFIX}"
    "${FLTK_BASE_DIR}/${FLTK_LIB_DIR}/${FLTK_LIB_PREFIX}fltk_forms${FLTK_LIB_SUFFIX}"
    "${FLTK_BASE_DIR}/${FLTK_LIB_DIR}/${FLTK_LIB_PREFIX}fltk_png${FLTK_LIB_SUFFIX}"
    "${FLTK_BASE_DIR}/${FLTK_LIB_DIR}/${FLTK_LIB_PREFIX}fltk_z${FLTK_LIB_SUFFIX}"
    ${FLTK_ADDITIONAL_LIBRARIES}
    PARENT_SCOPE
)
